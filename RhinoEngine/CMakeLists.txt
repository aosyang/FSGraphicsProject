CMAKE_MINIMUM_REQUIRED(VERSION 3.1)

PROJECT(RhinoEngine)

# Recursively find all .cpp and .h files
FILE(GLOB SRC
	 "*.cpp"
	 "*.h"
)

FILE(GLOB_RECURSE SRC_COLLISION
	 "Collision/*.cpp"
	 "Collision/*.h"
)

FILE(GLOB_RECURSE SRC_CORE
	 "Core/*.cpp"
	 "Core/*.h"
)

FILE(GLOB_RECURSE SRC_NAVIGATION
	 "Navigation/*.cpp"
	 "Navigation/*.h"
)

FILE(GLOB_RECURSE SRC_PHYSICS
	 "Physics/*.cpp"
	 "Physics/*.h"
)

FILE(GLOB_RECURSE SRC_RENDERSYSTEM
	 "RenderSystem/*.cpp"
	 "RenderSystem/*.h"
)

FILE(GLOB_RECURSE SRC_HLSL
	 "HLSL/*.hlsl"
)

# Shader compilation options
FOREACH(Shader ${SRC_HLSL})
	SET_SOURCE_FILES_PROPERTIES(${Shader} PROPERTIES
		VS_SHADER_TYPE Vertex
		VS_SHADER_MODEL 4.0
		VS_SHADER_ENTRYPOINT main
		VS_SHADER_OBJECT_FILE_NAME " "
		VS_SHADER_VARIABLE_NAME "%(Filename)"
		VS_SHADER_OUTPUT_HEADER_FILE "${CMAKE_SOURCE_DIR}/RhinoEngine/%(Filename).csh"
		VS_SHADER_ENABLE_DEBUG $<$<CONFIG:Debug>:true>
		VS_SHADER_DISABLE_OPTIMIZATIONS $<$<CONFIG:Debug>:true>
	)
ENDFOREACH()

FILE(GLOB_RECURSE SRC_IMGUI
	 "imgui/*.cpp"
	 "imgui/*.h"
)

FILE(GLOB_RECURSE SRC_RESOURCE
	 "Resource/*.cpp"
	 "Resource/*.h"
)

FILE(GLOB_RECURSE SRC_SCENE
	 "Scene/*.cpp"
	 "Scene/*.h"
)

FILE(GLOB_RECURSE SRC_SCRIPT
	 "Script/*.cpp"
	 "Script/*.h"
)

FILE(GLOB_RECURSE SRC_TINYXML2
	 "tinyxml2/*.cpp"
	 "tinyxml2/*.h"
)

SET(SRC_LUA_DIR ${CMAKE_SOURCE_DIR}/lua5.3)
FILE(GLOB_RECURSE SRC_LUA5_3
	 "${SRC_LUA_DIR}/*.c"
	 "${SRC_LUA_DIR}/*.h"
	 "${SRC_LUA_DIR}/*.hpp"
)

# Exclude lua source codes with a main function too avoid linking errors
LIST(REMOVE_ITEM SRC_LUA5_3 "${SRC_LUA_DIR}/lua.c")
LIST(REMOVE_ITEM SRC_LUA5_3 "${SRC_LUA_DIR}/luac.c")

INCLUDE_DIRECTORIES("." ${CMAKE_SOURCE_DIR}/fbx/include ${CMAKE_SOURCE_DIR}/ThirdParty/Bullet3/src)

ADD_LIBRARY(${PROJECT_NAME} STATIC
	${SRC}
	${SRC_COLLISION}
	${SRC_CORE}
	${SRC_NAVIGATION}
	${SRC_PHYSICS}
	${SRC_RENDERSYSTEM}
	${SRC_HLSL}
	${SRC_IMGUI}
	${SRC_RESOURCE}
	${SRC_SCENE}
	${SRC_SCRIPT}
	${SRC_TINYXML2}
	${SRC_LUA5_3}
)

TARGET_LINK_LIBRARIES(${PROJECT_NAME} libfbxsdk.lib)

# Generate filters for the project file
SOURCE_GROUP("" FILES ${SRC})
SOURCE_GROUP("Collision" FILES ${SRC_COLLISION})
SOURCE_GROUP("Core" FILES ${SRC_CORE})
SOURCE_GROUP("Navigation" FILES ${SRC_NAVIGATION})
SOURCE_GROUP("Physics" FILES ${SRC_PHYSICS})
SOURCE_GROUP("RenderSystem" FILES ${SRC_RENDERSYSTEM})
SOURCE_GROUP("RenderSystem\\HLSL" FILES ${SRC_HLSL})
SOURCE_GROUP("imgui" FILES ${SRC_IMGUI})
SOURCE_GROUP("Resource" FILES ${SRC_RESOURCE})
SOURCE_GROUP("Scene" FILES ${SRC_SCENE})
SOURCE_GROUP("Script" FILES ${SRC_SCRIPT})
SOURCE_GROUP("tinyxml2" FILES ${SRC_TINYXML2})
SOURCE_GROUP("lua5.3" FILES ${SRC_LUA5_3})

SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES FOLDER Engine)
